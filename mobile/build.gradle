/*
 * Copyright 2017 Diego Rossi (@_HellPie)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 *  limitations under the License.
 */

apply plugin: 'com.android.application'

apply from: '../tools.gradle'

def _signing = project.ext.getEnv('./signing.properties')
def _version = project.ext.getVersionInfo(project.ext.getGitInfo())

android {

	compileSdkVersion 26
	buildToolsVersion '26.0.1'

	flavorDimensions 'dev.hellpie.apps.tincrush'

	defaultConfig {

		applicationId 'dev.hellpie.apps.tincrush'
		minSdkVersion 23
		targetSdkVersion 26

		versionCode 0
		versionName _version.buildVersionName()

//		testApplicationId '${applicationId}.test'
//		testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'

		flavorSelection 'dev.hellpie.apps.tincrush', 'dev.hellpie.apps.tincrush'
	}

	signingConfigs {

		debug {
			keyAlias _signing.get('KEY_DEBUG_ALIAS_USERNAME')
			keyPassword _signing.get('KEY_DEBUG_ALIAS_PASSWORD')
			storeFile file(_signing.get('KEY_DEBUG_PATH'))
			storePassword _signing.get('KEY_DEBUG_PASSWORD')
		}

		publish {
			keyAlias _signing.get('KEY_RELEASE_ALIAS_USERNAME')
			keyPassword _signing.get('KEY_RELEASE_ALIAS_PASSWORD')
			storeFile file(_signing.get('KEY_RELEASE_PATH'))
			storePassword _signing.get('KEY_RELEASE_PASSWORD')
		}
	}

	buildTypes {

		debug {
			signingConfig signingConfigs.debug
		}

		prerelease {
			signingConfig signingConfigs.publish

			minifyEnabled true
			shrinkResources true
			useProguard false // Disable obfuscation, use Android Studio bundled Code Shrinking
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}

		release {
			signingConfig signingConfigs.publish

			minifyEnabled true
			shrinkResources true
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
	}

	productFlavors {

		debugFlavor {
			applicationIdSuffix '.debug'
			versionNameSuffix _version.buildVersionNameSuffix('debug')
			versionCode _version.buildVersionCode(7) as int
		}

		prereleaseFlavor {
			versionNameSuffix _version.buildVersionNameSuffix('prerelease')
			versionCode _version.buildVersionCode(5) as int
		}

		releaseFlavor {
			versionCode _version.buildVersionCode(2) as int
		}
	}

	variantFilter {variant ->
		variant.flavors.each {flavor ->
			if(flavor.name != "${variant.buildType.name}Flavor") variant.setIgnore(true)
		}
	}

	dexOptions {
		preDexLibraries true
	}

	dataBinding {
		enabled true
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
}

dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar'])

	implementation 'com.android.support:support-annotations:26.0.1'
	implementation 'com.android.support:support-core-utils:26.0.1'

	implementation 'com.android.support:appcompat-v7:26.0.1'

	implementation 'com.android.support:gridlayout-v7:26.0.1'
	implementation 'com.android.support:recyclerview-v7:26.0.1'

	implementation 'com.android.support:design:26.0.1'
	implementation 'com.android.support:cardview-v7:26.0.1'

	implementation 'com.android.support:palette-v7:26.0.1'

	implementation 'com.android.support:customtabs:26.0.1'

	implementation 'android.arch.lifecycle:runtime:1.0.0-alpha9'
	implementation 'android.arch.lifecycle:extensions:1.0.0-alpha9'
	annotationProcessor 'android.arch.lifecycle:compiler:1.0.0-alpha9'

	implementation 'android.arch.persistence.room:runtime:1.0.0-alpha9'
	annotationProcessor 'android.arch.persistence.room:compiler:1.0.0-alpha9'
//	testImplementation 'android.arch.persistence.room:testing:1.0.0-alpha9'

//	wearApp project(':wear')
//	implementation 'com.google.android.gms:play-services-wearable:11.2.0'

//	testImplementation 'junit:junit:4.12' 'junit:junit:4.12'
//	androidTestImplementation 'com.android.support.test:runner:1.0.0'
//	androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.0'
}
